#definicao do mapa de enderecamento
.eqv Buffer0Teclado     0xFFFF0100
.eqv Buffer1Teclado     0xFFFF0104
.eqv TecladoxMouse      0xFFFF0110
.eqv BufferMouse        0xFFFF0114
.eqv RXRS232            0xFFFF0120
.eqv TXRS232            0xFFFF0121
.eqv CTRLRS232          0xFFFF0122
.eqv LINHA1             0xFFFF0130
.eqv LINHA2             0xFFFF0140
.eqv LCDClear           0xFFFF0150
.eqv AudioINL           0xFFFF0000
.eqv AudioINR           0xFFFF0004
.eqv AudioOUTL          0xFFFF0008
.eqv AudioOUTR          0xFFFF000c
.eqv AudioCTRL1         0xFFFF0010
.eqv AudioCTRL2         0xFFFF0014
.eqv SD_BUFFER_INI      0xFFFF0250
.eqv SD_BUFFER_END      0xFFFF044C
.eqv SD_INTERFACE_ADDR  0xFFFF0450
.eqv SD_INTERFACE_CTRL  0xFFFF0454

# Sintetizador - 2015/1

.eqv NoteData           0xFFFF0200
.eqv NoteClock          0xFFFF0204
.eqv NoteMelody         0xFFFF0208
.eqv MusicTempo         0xFFFF020C
.eqv MusicAddress       0xFFFF0210

.eqv VGAADDRESSINI      0xFF000000
.eqv VGAADDRESSFIM      0xFF012C00
.eqv NUMLINHAS          240
.eqv NUMCOLUNAS         320


    .kdata   # endereço 0x9000 0000
kdata:
inicioKdata:
# Tabela de caracteres
# Endereço da Tabela de Caracteres
LabelTabChar:
.word 0x00000000, 0x00000000, 0x10101010, 0x00100010, 0x00002828, 0x00000000, 0x28FE2828, 0x002828FE, 0x38503C10, 0x00107814, 0x10686400, 0x00004C2C, 0x28102818, 0x003A4446, 0x00001010, 0x00000000, 0x20201008, 0x00081020, 0x08081020, 0x00201008, 0x38549210, 0x00109254, 0xFE101010, 0x00101010, 0x00000000, 0x10081818, 0xFE000000, 0x00000000, 0x00000000, 0x18180000, 0x10080402, 0x00804020, 0x54444438, 0x00384444, 0x10103010, 0x00381010, 0x08044438, 0x007C2010, 0x18044438, 0x00384404, 0x7C482818, 0x001C0808, 0x7840407C, 0x00384404, 0x78404438, 0x00384444, 0x1008047C, 0x00202020, 0x38444438, 0x00384444, 0x3C444438, 0x00384404, 0x00181800, 0x00001818, 0x00181800, 0x10081818, 0x20100804, 0x00040810, 0x00FE0000, 0x000000FE, 0x04081020, 0x00201008, 0x08044438, 0x00100010, 0x545C4438, 0x0038405C, 0x7C444438, 0x00444444, 0x78444478, 0x00784444, 0x40404438, 0x00384440, 0x44444478, 0x00784444, 0x7840407C, 0x007C4040, 0x7C40407C, 0x00404040, 0x5C404438, 0x00384444, 0x7C444444, 0x00444444, 0x10101038, 0x00381010, 0x0808081C, 0x00304848, 0x70484444, 0x00444448, 0x20202020, 0x003C2020, 0x92AAC682, 0x00828282, 0x54546444, 0x0044444C, 0x44444438, 0x00384444, 0x38242438, 0x00202020, 0x44444438, 0x0C384444, 0x78444478, 0x00444850, 0x38404438, 0x00384404, 0x1010107C, 0x00101010, 0x44444444, 0x00384444, 0x28444444, 0x00101028, 0x54828282, 0x00282854, 0x10284444, 0x00444428, 0x10284444, 0x00101010, 0x1008047C, 0x007C4020, 0x20202038, 0x00382020, 0x10204080, 0x00020408, 0x08080838, 0x00380808, 0x00442810, 0x00000000, 0x00000000, 0xFE000000, 0x00000810, 0x00000000, 0x3C043800, 0x003A4444, 0x24382020, 0x00582424, 0x201C0000, 0x001C2020, 0x48380808, 0x00344848, 0x44380000, 0x0038407C, 0x70202418, 0x00202020, 0x443A0000, 0x38043C44, 0x64584040, 0x00444444, 0x10001000, 0x00101010, 0x10001000, 0x60101010, 0x28242020, 0x00242830, 0x08080818, 0x00080808, 0x49B60000, 0x00414149, 0x24580000, 0x00242424, 0x44380000, 0x00384444, 0x24580000, 0x20203824, 0x48340000, 0x08083848, 0x302C0000, 0x00202020, 0x201C0000, 0x00380418, 0x10381000, 0x00101010, 0x48480000, 0x00344848, 0x44440000, 0x00102844, 0x82820000, 0x0044AA92, 0x28440000, 0x00442810, 0x24240000, 0x38041C24, 0x043C0000, 0x003C1008, 0x2010100C, 0x000C1010, 0x10101010, 0x00101010, 0x04080830, 0x00300808, 0x92600000, 0x0000000C, 0x243C1818, 0xA55A7E3C, 0x99FF5A81, 0x99663CFF, 0x10280000, 0x00000028, 0x10081020, 0x00081020

# scancode -> ascii
LabelScanCode:
.word 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,0x00, 0x00, 0x00, 0x00, 0x00, 0x71, 0x31, 0x00, 0x00, 0x00, 0x7a, 0x73, 0x61, 0x77, 0x32, 0x00,0x00, 0x63, 0x78, 0x64, 0x65, 0x34, 0x33, 0x00, 0x00, 0x00, 0x76, 0x66, 0x74, 0x72, 0x35, 0x00,0x00, 0x6e, 0x62, 0x68, 0x67, 0x79, 0x36, 0x00, 0x00, 0x00, 0x6d, 0x6a, 0x75, 0x37, 0x38, 0x00,0x00, 0x2c, 0x6b, 0x69, 0x6f, 0x30, 0x39, 0x00, 0x00, 0x2e, 0x2f, 0x6c, 0x3b, 0x70, 0x2d, 0x00,0x00, 0x00, 0x27, 0x00, 0x00, 0x3d, 0x00, 0x00, 0x00, 0x00, 0x00, 0x5b, 0x00, 0x5d, 0x00, 0x00,0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x31, 0x00, 0x34, 0x37, 0x00, 0x00, 0x00,0x30, 0x2e, 0x32, 0x35, 0x36, 0x38, 0x00, 0x00, 0x00, 0x2b, 0x33, 0x2d, 0x2a, 0x39, 0x00, 0x00,0x00, 0x00, 0x00, 0x00, 0x00, 0x00
# scancode -> ascii (com shift)
.word 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,0x00, 0x00, 0x00, 0x00, 0x00, 0x51, 0x21, 0x00, 0x00, 0x00, 0x5a, 0x53, 0x41, 0x57, 0x40, 0x00,0x00, 0x43, 0x58, 0x44, 0x45, 0x24, 0x23, 0x00, 0x00, 0x00, 0x56, 0x46, 0x54, 0x52, 0x25, 0x00,0x00, 0x4e, 0x42, 0x48, 0x47, 0x59, 0x5e, 0x00, 0x00, 0x00, 0x4d, 0x4a, 0x55, 0x26, 0x2a, 0x00,0x00, 0x3c, 0x4b, 0x49, 0x4f, 0x29, 0x28, 0x00, 0x00, 0x3e, 0x3f, 0x4c, 0x3a, 0x50, 0x5f, 0x00,0x00, 0x00, 0x22, 0x00, 0x00, 0x2b, 0x00, 0x00, 0x00, 0x00, 0x00, 0x7b, 0x00, 0x7d, 0x00, 0x00,0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,0x00, 0x00, 0x00, 0x00, 0x00, 0x00

instructionMessage:     .ascii  "   Instrucao    "
                        .asciiz "   Invalida!    "
lixobuffer:             .asciiz "  "  # so para alinhas o endereco do FloatBuffer!
#buffer do ReadFloatm 32 caracteres
FloatBuffer:            .asciiz "                                "

# variaveis para implementar a fila de eventos de input
eventQueueBeginAddr:    .word 0x90000E00
eventQueueEndAddr:      .word 0x90001000
eventQueueBeginPtr:     .word 0x90000E00
eventQueueEndPtr:       .word 0x90000E00


#MOUSE
DATA_X:         .word 0
DATA_Y:         .word 0
DATA_CLICKS:    .word 0


##### Preparado para considerar syscall = jal ktext  para o pipeline

    .ktext

exceptionHandling:
    addi    $sp, $sp, -8
    sw      $at, 0($sp)
    sw      $ra, 4($sp)
    addi    $k0, $zero, 32              # default syscall exception=8*4
    mfc0    $k0, $13                    # nao esta implementada no pipe
    nop                                 # nao retirar!
    andi    $k0, $k0, 0x007C
    srl     $k0, $k0, 2

    addi    $k1, $zero, 12              # overflow na ULA
    beq     $k1, $k0, ALUOverflowException

    addi    $k1, $zero, 15              # excecao de ponto flutuante
    beq     $k1, $k0, FPALUException

    addi    $k1, $zero, 0               # interrupcao
    beq     $k1, $k0, interruptException

    addi    $k1, $zero, 10              # instrucao reservada ou invalida
    beq     $k1, $k0, instructionException

    addi    $k1, $zero, 8               # syscall
    beq     $k1, $k0, syscallException

endException:
    lw      $ra, 4($sp)
    lw      $at, 0($sp)
    addi    $sp, $sp, 8

    mfc0    $k0, $14                    # EPC + 4     //NOTE: nao esta implementada no pipe
    addi    $k0, $k0, 4
    mtc0    $k0, $14                    #nao esta implementada no pipe
    eret                                #nao esta implementada no pipe
    jr      $ra                         #para o caso do eret nao estar implementado (pipeline)

ALUOverflowException:   j endException  # escolhi nao fazer nada, ja que ate hoje nunca vi um SO tratar esse tipo de excecao...

FPALUException:         j endException  # escolhi nao fazer nada, ja que ate hoje nunca vi um SO tratar esse tipo de excecao...


interruptException:
    mfc0    $k0, $13
    andi    $k0, $k0, 0xFF00
    srl     $k0, $k0, 8

    andi    $k1, $k0, 0x0001
    bne     $k1, $zero, keyboardInterrupt

    andi    $k1, $k0, 0x0002
    bne     $k1, $zero, audioInterrupt

    andi    $k1, $k0, 0x0004
    bne     $k1, $zero, mouseInterrupt

    andi    $k1, $k0, 0x0008            #verificar se nao  seria 0x0008     NOTE: Mas está 0x0008!
    bne     $k1, $zero, counterInterrupt

    j       endException

counterInterrupt:   j endException      # nenhum tratamento para a interrupcao de contagem eh necessario ate agora

audioInterrupt:     j endException      # TODO: Implementar interrupção de áudio.

###########  Interrupcao do Mouse ################
mouseInterrupt:
    la      $k0, BufferMouse            # endereço do buffer_mouse
    nop
    nop
    lw      $k0, 0($k0)                 # carrega o buffer em k0
    nop
    nop
    andi    $k1, $k0, 0xFF

    addi    $sp, $sp, -8
    sw      $t0, 0($sp)
    sw      $t1, 4($sp)

    #default (supõe-se que seja o movimento do mouse)

    ###atualiza os clicks
    li      $k1, 0x00ff0000
    nop
    nop
    and     $k1, $k0, $k1
    nop
    nop
    srl     $k1, $k1, 16                # k1 tem o byte com info dos clicks e sinais de X/Y
    andi    $t0, $k1, 1                 # $t0=botão esquerdo
    li      $t1, 0
    beq     $t0, $zero, MOUSEPULAESQ
    li      $t1, 0xF

MOUSEPULAESQ:
    andi    $t0, $k1, 2
    nop
    nop
    srl     $t0, $t0, 1                 # $t0=botão direito
    beq     $t0, $zero, MOUSEPULADIR
    ori     $t1, $t1, 0xF0

MOUSEPULADIR:
    andi    $t0, $k1, 4
    srl     $t0, $t0, 2                 #$t0=botão do meio
    beq     $t0, $zero, MOUSEPULAMEIO
    ori     $t1, $t1, 0xF00
    nop
    nop
MOUSEPULAMEIO:
    sw      $t1, DATA_CLICKS($zero)     # FIXME: ENDERECO ERRADO!!!!!!!

    ###atualiza o x
    andi    $t0, $k1, 0x10
    nop
    nop
    srl     $t0, $t0, 4                 #t0=(sinal)
    la      $t1, 0x0000ff00
    nop
    nop
    and     $t1, $t1, $k0
    srl     $t1, $t1, 8
    beq     $t0, $zero, pulasinalmousex
    la      $t0, 0xffffff00
    nop
    nop
    or      $t1, $t1, $t0

pulasinalmousex:
    lw      $t0, DATA_X($zero)          # FIXME: ENDERECO ERRADO
    nop
    nop
    add     $t0, $t0, $t1
    li      $t1, 320
    nop
    nop
    slt     $t1, $t0, $t1
    bne     $t1, $zero, mouseliberax320
    li      $t0, 320

mouseliberax320:
    li      $t1, 0
    slt     $t1, $t0, $t1
    beq     $t1, $zero, mouseliberax0
    li      $t0, 0

mouseliberax0:
    sw      $t0, DATA_X($zero)          # FIXME: ENDEREÇO ERRADO

    ###atualiza o Y
    andi    $t0, $k1, 0x20
    nop
    nop
    srl     $t0, $t0, 5                 #t0=(sinal)
    la      $t1, 0x000000ff
    and     $t1, $t1, $k0
    beq     $t0, $zero, pulasinalmousey
    la      $t0, 0xffffff00
    nop
    nop
    or      $t1, $t1, $t0

    #t1=-delta y
pulasinalmousey:
    nor     $t1, $t1, $t1
    addi    $t1, $t1, 1                 #t1=delta y
    lw      $t0, DATA_Y($zero)          # FIXME: ENDERECO ERRADO
    add     $t0, $t0, $t1
    li      $t1, 240
    nop
    nop
    slt     $t1, $t0, $t1
    bne     $t1, $zero, mouseliberay240
    li      $t0, 240

mouseliberay240:
    li      $t1, 0
    nop
    nop
    slt     $t1, $t0, $t1
    beq     $t1, $zero, mouseliberay0
    li      $t0, 0

mouseliberay0:
    sw      $t0, DATA_Y($zero)          # FIXME: END ERRADO
    nop
    nop
    lw      $t0, 0($sp)
    lw      $t1, 4($sp)
    addi    $sp, $sp, 8

    j endException


############### Interrupcao do teclado ##################
keyboardInterrupt:
    addi    $sp, $sp, -8
    sw      $a0, 0($sp)
    sw      $v0, 4($sp)

    # verifica se ha espaco na fila comparando o incremento do ponteiro do fim da fila com o ponteiro do inicio
    la      $a0, eventQueueEndPtr
    nop
    nop
    lw      $a0, 0($a0)
    jal     eventQueueIncrementPointer
    la      $k0, eventQueueBeginPtr
    nop
    nop
    lw      $k0, 0($k0)
    nop
    nop
    beq     $k0, $v0, keyboardInterruptEnd

    # FIXME: preparar o evento de teclado no registrador $k0
    la      $k0, Buffer0Teclado
    nop
    nop
    lw      $k0, 0($k0)
    nop
    nop
    move    $t9, $k0

    # poe o evento de teclado na fila
    sw      $k0, 0($a0)
    la      $k0, eventQueueEndPtr
    nop
    nop
    sw      $v0, 0($k0)

keyboardInterruptEnd:
    lw      $a0, 0($sp)
    lw      $v0, 4($sp)
    addi    $sp, $sp, 8

    j       endException

    # $a0 = endereco (ou ponteiro) a ser incrementado. $v0 = valor incrementado
eventQueueIncrementPointer:
    addi    $v0, $a0, 4
    la      $t0, eventQueueEndAddr
    nop
    nop
    lw      $t0, 0($t0)
    nop
    nop
    beq     $t0, $v0, eventQueueIncrementPointerIf
    jr      $ra

eventQueueIncrementPointerIf:
    la      $v0, eventQueueBeginAddr
    nop
    nop
    lw      $v0, 0($v0)
    jr      $ra


########  Interrupcao de Instrucao Invalida  ###########
    # mostra mensagem de erro no display LCD
instructionException:
    la      $t0, instructionMessage
    nop
    nop
    la      $t9, LINHA1
    nop
    nop
    sw      $zero, 0x20($t9)
    lb      $t1, 0($t0)                 # primeiro caractere
    nop
    nop
instructionExceptionLoop:
    beq     $t1, $zero, goToExit        # fim da string
    sb      $t1, 0($t9)
    addi    $t0, $t0, 1
    addi    $t9, $t9, 1
    lb      $t1, 0($t0)
    j       instructionExceptionLoop



############# interrupcao de SYSCALL ###################
syscallException:
    addi    $sp, $sp, -264              # Salva todos os registradores na pilha
    sw      $1,     0($sp)
    sw      $2,     4($sp)
    sw      $3,     8($sp)
    sw      $4,    12($sp)
    sw      $5,    16($sp)
    sw      $6,    20($sp)
    sw      $7,    24($sp)
    sw      $8,    28($sp)
    sw      $9,    32($sp)
    sw      $10,   36($sp)
    sw      $11,   40($sp)
    sw      $12,   44($sp)
    sw      $13,   48($sp)
    sw      $14,   52($sp)
    sw      $15,   56($sp)
    sw      $16,   60($sp)
    sw      $17,   64($sp)
    sw      $18,   68($sp)
    sw      $19,   72($sp)
    sw      $20,   76($sp)
    sw      $21,   80($sp)
    sw      $22,   84($sp)
    sw      $23,   88($sp)
    sw      $24,   92($sp)
    sw      $25,   96($sp)
    sw      $26,  100($sp)
    sw      $27,  104($sp)
    sw      $28,  108($sp)
    sw      $29,  112($sp)
    sw      $30,  116($sp)
    sw      $31,  120($sp)
    swc1    $f0,  124($sp)
    swc1    $f1,  128($sp)
    swc1    $f2,  132($sp)
    swc1    $f3,  136($sp)
    swc1    $f4,  140($sp)
    swc1    $f5,  144($sp)
    swc1    $f6,  148($sp)
    swc1    $f7,  152($sp)
    swc1    $f8,  156($sp)
    swc1    $f9,  160($sp)
    swc1    $f10, 164($sp)
    swc1    $f11, 168($sp)
    swc1    $f12, 172($sp)
    swc1    $f13, 176($sp)
    swc1    $f14, 180($sp)
    swc1    $f15, 184($sp)
    swc1    $f16, 188($sp)
    swc1    $f17, 192($sp)
    swc1    $f18, 196($sp)
    swc1    $f19, 200($sp)
    swc1    $f20, 204($sp)
    swc1    $f21, 208($sp)
    swc1    $f22, 212($sp)
    swc1    $f23, 216($sp)
    swc1    $f24, 220($sp)
    swc1    $f25, 224($sp)
    swc1    $f26, 228($sp)
    swc1    $f27, 232($sp)
    swc1    $f28, 236($sp)
    swc1    $f29, 240($sp)
    swc1    $f30, 244($sp)
    swc1    $f31, 248($sp)
    # mthi, mtlo - 2015/1 (Salva os registradores HI e LO)
    mfhi    $t9
    sw      $t9, 252($sp)
    mflo    $t9
    sw      $t9, 256($sp)
    # Zera os valores dos registradores temporarios - 2015/1

    add     $t0, $zero, $zero
    add     $t1, $zero, $zero
    add     $t2, $zero, $zero
    add     $t3, $zero, $zero
    add     $t4, $zero, $zero
    add     $t5, $zero, $zero
    add     $t6, $zero, $zero
    add     $t7, $zero, $zero
    add     $t8, $zero, $zero
    add     $t9, $zero, $zero


    addi    $t0, $zero, 10
    beq     $t0, $v0, goToExit          # syscall exit

    addi    $t0, $zero, 1               # sycall 1 = print int
    beq     $t0, $v0, goToPrintInt
    addi    $t0, $zero, 101             # sycall 1 = print int
    beq     $t0, $v0, goToPrintInt

    addi    $t0, $zero, 2               # syscall 2 = print float
    beq     $t0, $v0, goToPrintFloat
    addi    $t0, $zero, 102             # syscall 2 = print float
    beq     $t0, $v0, goToPrintFloat

    addi    $t0, $zero, 4               # syscall 4 = print string
    beq     $t0, $v0, goToPrintString
    addi    $t0, $zero, 104             # syscall 4 = print string
    beq     $t0, $v0, goToPrintString

    addi    $t0, $zero, 5               # syscall 5 = read int
    beq     $t0, $v0, goToReadInt
    addi    $t0, $zero, 105             # syscall 5 = read int
    beq     $t0, $v0, goToReadInt

    addi    $t0, $zero, 6               # syscall 6 = read float
    beq     $t0, $v0, goToReadFloat
    addi    $t0, $zero, 106             # syscall 6 = read float
    beq     $t0, $v0, goToReadFloat

    addi    $t0, $zero, 8               # syscall 8 = read string
    beq     $t0, $v0, goToReadString
    addi    $t0, $zero, 108             # syscall 8 = read string
    beq     $t0, $v0, goToReadString

    addi    $t0, $zero, 11              # syscall 11 = print char
    beq     $t0, $v0, goToPrintChar
    addi    $t0, $zero, 111             # syscall 11 = print char
    beq     $t0, $v0, goToPrintChar

    addi    $t0, $zero, 12              # syscall 12 = read char
    beq     $t0, $v0, goToReadChar
    addi    $t0, $zero, 112             # syscall 12 = read char
    beq     $t0, $v0, goToReadChar

    #### colocar aqui as chamadas para os syscalls 31 e 33

    # syscall 30 = time     syscall 32 = sleep    syscall 41 = random      EM HARDWARE

    addi    $t0, $zero, 45              # syscall 45 = plot
    beq     $t0, $v0, goToPlot

    addi    $t0, $zero, 46              # syscall 46 = getplot
    beq     $t0, $v0, goToGetPlot
    # Sintetizador - 2015/1

    addi    $t0, $zero, 31              # syscall 31 = MIDI out
    beq     $t0, $v0, goToMidiOut       # Generate tone and return immediately
    # addi    $t0, $zero, 131             # syscall 31 = MIDI out
    # beq     $t0, $v0, goToMidiOut

    # syscall 32 = sleep
    # Combinado com o syscall 31 para gerar o syscall 33

    addi    $t0, $zero, 33              # syscall 33 = MIDI out synchronous
    beq     $t0, $v0, goToMidiOutSync   # Generate tone and return upon tone completion
    # addi    $t0, $zero, 133             # syscall 33 = MIDI out synchronous
    # beq     $t0, $v0, goToMidiOutSync

    addi    $t0, $zero, 45              # syscall 45 = plot
    beq     $t0, $v0, goToPlot

    addi    $t0, $zero, 46              # syscall 46 = getplot
    beq     $t0, $v0, goToGetPlot

    addi    $t0, $zero, 47              # syscall 47 = inkey
    beq     $t0, $v0, goToInKey

    addi    $t0, $zero, 48              # syscall 48 = CLS
    beq     $t0, $v0, goToCLS

    addi    $t0, $zero, 49              # syscall 49 = SD Card read
    beq     $t0, $v0, goToSDread

    addi    $t0, $zero, 150             # syscall 50 = pop event
    beq     $t0, $v0, goToPopEvent



endSyscall:                             # recupera todos os registradores na pilha
    lw      $1,     0($sp)
    # lw      $2,     4($sp)      # $v0 retorno de valor do syscall
    lw      $3,     8($sp)
    # lw      $4,    12($sp)      # $a0  as vezes usado como retorno
    # lw      $5,    16($sp)      # $a1
    lw      $6,    20($sp)
    lw      $7,    24($sp)
    lw      $8,    28($sp)
    lw      $9,    32($sp)
    lw      $10,   36($sp)
    lw      $11,   40($sp)
    lw      $12,   44($sp)
    lw      $13,   48($sp)
    lw      $14,   52($sp)
    lw      $15,   56($sp)
    lw      $16,   60($sp)
    lw      $17,   64($sp)
    lw      $18,   68($sp)
    lw      $19,   72($sp)
    lw      $20,   76($sp)
    lw      $21,   80($sp)
    lw      $22,   84($sp)
    lw      $23,   88($sp)
    lw      $24,   92($sp)
    lw      $25,   96($sp)
    lw      $26,  100($sp)
    lw      $27,  104($sp)
    lw      $28,  108($sp)
    lw      $29,  112($sp)
    lw      $30,  116($sp)
    lw      $31,  120($sp)
    # lwc1    $0,   124($sp)      # $f0 retorno de valor de syscall
    lwc1    $f1,  128($sp)
    lwc1    $f2,  132($sp)
    lwc1    $f3,  136($sp)
    lwc1    $f4,  140($sp)
    lwc1    $f5,  144($sp)
    lwc1    $f6,  148($sp)
    lwc1    $f7,  152($sp)
    lwc1    $f8,  156($sp)
    lwc1    $f9,  160($sp)
    lwc1    $f10, 164($sp)
    lwc1    $f11, 168($sp)
    lwc1    $f12, 172($sp)
    lwc1    $f13, 176($sp)
    lwc1    $f14, 180($sp)
    lwc1    $f15, 184($sp)
    lwc1    $f16, 188($sp)
    lwc1    $f17, 192($sp)
    lwc1    $f18, 196($sp)
    lwc1    $f19, 200($sp)
    lwc1    $f20, 204($sp)
    lwc1    $f21, 208($sp)
    lwc1    $f22, 212($sp)
    lwc1    $f23, 216($sp)
    lwc1    $f24, 220($sp)
    lwc1    $f25, 224($sp)
    lwc1    $f26, 228($sp)
    lwc1    $f27, 232($sp)
    lwc1    $f28, 236($sp)
    lwc1    $f29, 240($sp)
    lwc1    $f30, 244($sp)
    lwc1    $f31, 248($sp)
    # mthi, mtlo - 2015/1 (Recupera os registradores HI e LO)
    lw      $t9,  252($sp)
    mthi    $t9
    lw      $t9,  256($sp)
    mtlo    $t9
    lw      $t9,   96($sp)
    addi    $sp, $sp, 264

    j endException


goToExit1:
    la      $t9, LINHA1                 # escreve FIM no LCD  <= RETIREI mudar o goToExit1  NOTE: não entendir
    nop
    nop
    sb      $zero, 0x20($t9)            # limpa
    li      $t0, 0x46
    sb      $t0, 0x07($t9)
    li      $t0, 0x49
    sb      $t0, 0x08($t9)
    li      $t0, 0x4D
    sb      $t0, 0x09($t9)

goToExit:
    j       goToExit                    ########### syscall 10

goToPrintInt:
    jal     printInt                    # chama printInt
    j       endSyscall

goToPrintString:
    jal     printString                 # chama printString
    j       endSyscall

goToPrintChar:
    jal     printChar                   # chama printChar
    j       endSyscall

goToPrintFloat:
    la      $s6, FloatBuffer            # add $s6, $zero, $zero        #chama float e printstring
    jal     printFloat
    la      $a0, FloatBuffer
    jal     printString
    j       endSyscall

goToPlot:
    jal     Plot
    j       endSyscall

goToGetPlot:
    jal     GetPlot
    j       endSyscall

goToReadChar:
    jal     readChar                    # chama readChar
    j       endSyscall

goToReadInt:
    jal     readInt                     # chama readInt
    j       endSyscall

goToReadString:
    jal     readString                  # chama readString
    j       endSyscall

goToReadFloat:
    jal     readFloat                   # chama readFloat
backReadFloat:
    j       endSyscall

# Sintetizador - 2015/1

goToMidiOut:
    jal     midiOut                     # chama MIDIout
    j       endSyscall

goToMidiOutSync:
    jal     midiOutSync                 # chama MIDIoutSync
    j       endSyscall

goToInKey:
    jal     inKey                       # chama inKey
    j       endSyscall

goToCLS:
    jal     CLS                         # chama CLS
    j       endSyscall

goToSDread:
    jal     sdRead                      # Chama sdRead
    j       endSyscall

goToPopEvent:
    jal     popEvent                    # chama popEvent
    j       endSyscall




#############################################
#  PrintInt                                 #
#  $a0    =    valor inteiro                #
#  $a1    =    x                            #
#  $a2    =    y                            #
#############################################

printInt:
    addi    $sp, $sp, -4                # salva $ra
    sw      $ra, 0($sp)

    bne     $a0, $zero, printNotZero    # chama printNotZero

printZero:
    addi    $a0, $zero, 48              # Imprime 0
    jal     printChar

printIntEnd:
    lw      $ra, 0($sp)                 # retorna
    addi    $sp, $sp, 4
    nop
    nop
    jr      $ra

printNotZero:
    add     $t0, $zero, $a0             # $t0 contem o valor do inteiro a ser impresso
    addi    $t1, $zero, 10              # $t1 eh uma constante 10
    slt     $t9, $t0, $zero             # $t0 < 0 ?
    beq     $t9, $zero, PrintIntContinue        # verifica se o valor eh negativo.

    addi    $a0, $zero, 45              # Negativo, imprime um '-' na tela

    addi    $sp, $sp, -12
    sw      $ra, 8($sp)
    sw      $t1, 4($sp)
    sw      $t0, 0($sp)                 # salva regs

    jal     printChar                   # imprime ASCII 45

    lw      $ra, 8($sp)                 # recupera regs
    lw      $t1, 4($sp)
    lw      $t0, 0($sp)
    addi    $sp, $sp, 12

    sub     $t0, $zero, $t0             # Torna $t0 positivo
    addi    $a1, $a1, 8                 # incrementa a coluna
    add     $t3, $zero, $zero           # $t3=0

PrintIntContinue:
    beq     $t0, $zero, PrintIntPop     # se $t0 é zero, nao há mais digitos para imprimir

    div     $t0, $t1                    # divide o valor por 10
    nop
    nop
    mflo    $t0                         # $t0 contem o valor dividido por 10
    mfhi    $t2                         # $t2 contem o ultimo digito a ser impresso
    nop
    nop
    addi    $sp, $sp, -4
    sw      $t2, 0($sp)                 # empilha $t2

    addi    $t3, $t3, 1                 # conta quantos elementos (digitos) estão na pilha
    j       PrintIntContinue            # volta para ser dividido e empilhado de novo

PrintIntPop:
    beq     $t3, $zero, printIntEnd     # ultimo digito endPrintInt

    lw      $a0, 0($sp)                 # le valor da pilha e coloca em $a0
    addi    $sp, $sp, 4
    nop                                 # hazard lw addi
    addi    $a0, $a0, 48                # código ASCII do dígito = numero + 48
    nop
    addi    $sp, $sp, -8                # salva regs
    sw      $t3, 0($sp)
    sw      $ra, 4($sp)

    jal     printChar                   # imprime digito

    lw      $ra, 4($sp)                 # recupera regs
    lw      $t3, 0($sp)
    addi    $sp, $sp, 8

    addi    $a1, $a1, 8                 # incrementa a coluna
    addi    $t3, $t3, -1                # decrementa contador
    j       PrintIntPop                 # volta

#endPrintInt:                 # recupera $ra
#        lw $ra, -4($sp)
#        jr $ra                            # fim printInt



#####################################
#  PrintSring                       #
#  $a0    =  endereco da string     #
#  $a1    =  x                      #
#  $a2    =  y                      #
#####################################

printString:
    addi    $sp, $sp, -4                # salva $ra
    sw      $ra, 0($sp)

    move    $t0, $a0                    # $t0=endereco da string

ForPrintString:
    lb      $a0, 0($t0)                 # le em $a0 o caracter a ser impresso

#     move    $k0, $zero                  # contador 4 bytes
# Loop4bytes:
#     slti    $k1, $k0, 4
#     beq     $k1, $zero, Fim4bytes
#
#     andi    $k1, $a0, 0x00FF
    beq     $a0, $zero, EndForPrintString   # string ASCIIZ termina com NULL

    addi    $sp, $sp, -4                # salva $t0
    sw      $t0, 0($sp)
    # sw      $a0, 4($sp)
    # andi    $a0, $a0, 0x00FF

    jal     printChar                   # imprime char

#        lw $a0, 4($sp)
    lw      $t0, 0($sp)                 # recupera $t0
    addi    $sp, $sp, 4


    addi    $a1, $a1, 8                 # incrementa a coluna
    nop
    nop
    slti    $k1, $a1, 313               # 320-8
    nop
    nop
    bne     $k1, $zero, NaoPulaLinha
    addi    $a2, $a2, 8                 # incrementa a linha
    move    $a1, $zero

NaoPulaLinha:
    # srl     $a0, $a0, 8                 # proximo byte
    # addi    $k0, $k0, 1                 #incrementa contador 4 bytes
    # j       Loop4bytes

# Fim4bytes:
#     addi    $t0, $t0, 4                 # Proxima word da memoria
    addi    $t0, $t0, 1
    j       ForPrintString              # loop

EndForPrintString:
    lw      $ra, 0($sp)                 # recupera $ra
    addi    $sp, $sp, 4
    nop
    jr      $ra                         # fim printString


#########################################################
#  PrintChar                                            #
#  $a0 = char(ASCII)                                    #
#  $a1 = x                                              #
#  $a2 = y                                              #
#########################################################
#   $t0 = i                                             #
#   $t1 = j                                             #
#   $t2 = endereco do char na memoria                   #
#   $t3 = metade do char (2a e depois 1a)               #
#   $t4 = endereco para impressao                       #
#   $t5 = background color                              #
#   $t6 = foreground color                              #
#   $t7 = 2                                             #
#########################################################

#printChar:    andi $t5,$a3,0xFF00                # cor fundo
#        andi $t6,$a3,0x00FF                # cor frente
#        srl $t5,$t5,8

         #li $t7, 2                    # iniciando $t7=2
printChar:
    andi    $t5, $a3, 0xFF00             # cor fundo
    andi    $t6, $a3, 0x00FF             # cor frente
    srl     $t5, $t5, 8

    slti    $t4, $a0, 32
    bne     $t4, $zero, NAOIMPRIMIVEL
    slti    $t4, $a0, 126
    beq     $t4, $zero, NAOIMPRIMIVEL
    j       IMPRIMIVEL
NAOIMPRIMIVEL:
    li      $a0, 32

#        addi $t4, $a2, 0                # t4 = y
#        sll $t4, $t4, 8                    # t4 = 256(y)
IMPRIMIVEL:
    li      $at, NUMCOLUNAS
    mult    $at, $a2
    nop
    nop
    mflo    $t4
    nop
    nop
    add     $t4, $t4, $a1               # t4 = 256(y) + x
    nop
    nop
    addi    $t4, $t4, 7                 # t4 = 256(y) + (x+7)
    la      $t8, VGAADDRESSINI          # Endereco de inicio da memoria VGA
    nop
    nop
    add     $t4, $t4, $t8               # t4 = endereco de impressao do ultimo pixel da primeira linha do char

    addi    $t2, $a0, -32               # indice do char na memoria
    nop
    nop
    sll     $t2, $t2, 3                 # offset em bytes em relacao ao endereco inicial

    la      $t3, kdata
    nop
    nop
    add     $t2, $t2, $t3               # pseudo .kdata
#        addi $t2, $t2, 0x00004b40        # endereco do char na memoria  ###########################################################
    nop
    nop
    lw      $t3, 0($t2)                 # carrega a primeira word do char
    addi    $t0, $zero, 4               # i = 4
    nop
    nop
forChar1I:
    beq     $t0, $zero, endForChar1I    # if(i == 0) end for i
    addi    $t1, $zero, 8               # j = 8
    nop
    nop

    forChar1J:
        beq     $t1, $zero, endForChar1J    # if(j == 0) end for j

#        div $t3, $t7
        andi    $t9, $t3, 0x0001
        srl     $t3, $t3, 1             # t3 = t3/2  ???????????????????
#        mfhi $t9                       # t9 = t3%
        beq     $t9, $zero, printCharPixelbg1
        sb      $t6, 0($t4)             # imprime pixel com cor de frente
        j       endCharPixel1
printCharPixelbg1:
    sb      $t5, 0($t4)                 # imprime pixel com cor de fundo
endCharPixel1:
    addi    $t1, $t1, -1                # j--
    addi    $t4, $t4, -1                # t4 aponta um pixel para a esquerda
    j       forChar1J

endForChar1J:
    addi    $t0, $t0, -1                # i--
    # addi    $t4, $t4, 264               # t4 = t4 + 8 + 256 (t4 aponta para o ultimo pixel da linha de baixo)
    addi    $t4, $t4, 328               # 2**12 + 8
    j       forChar1I

endForChar1I:
    lw      $t3, 4($t2)                 # carrega a segunda word do char

    addi    $t0, $zero, 4               # i = 4
    nop
    nop
forChar2I:
    beq     $t0, $zero, endForChar2I    # if(i == 0) end for i
    addi    $t1, $zero, 8               # j = 8
    nop
    nop
    forChar2J:
        beq     $t1, $zero, endForChar2J    # if(j == 0) end for j

        # div     $t3, $t7
        andi    $t9, $t3, 0x0001
        srl     $t3, $t3, 1                 # t3 = t3/2
        # mfhi    $t9                         # t9 = t3%2
        beq     $t9, $zero, printCharPixelbg2
        sb      $t6, 0($t4)
        j       endCharPixel2

printCharPixelbg2:
    sb      $t5, 0($t4)

endCharPixel2:
    addi    $t1, $t1, -1                # j--
    addi    $t4, $t4, -1                # t4 aponta um pixel para a esquerda
    j       forChar2J

endForChar2J:
    addi    $t0, $t0, -1                # i--
    # addi    $t4, $t4, 264               # t4 = t4 + 8 + 256 (t4 aponta para o ultimo pixel da linha de baixo)
    addi    $t4, $t4, 328
    j       forChar2I

endForChar2I:
    jr $ra



#######################
#  Plot                  #
#  $a0    =  x          #
#  $a1    =  y          #
#  $a2    =  cor        #
#######################
# Obs.: Eh muito mais rapido usar diretamente no codigo!

Plot:
    li      $at, NUMCOLUNAS
    mult    $a1,$at
    nop
    nop
    mflo    $a1
    add     $a0,$a0,$a1
    la      $a1, VGAADDRESSINI          # endereco VGA
    nop
    nop
    or      $a0, $a0, $a1
    sb      $a2, 0($a0)
    jr      $ra


############################
#  GetPlot                 #
#  $a0 = x                 #
#  $a1 = y                 #
#  $a2 = cor               #
############################
# Obs.: Eh muito mais rapido usar diretamente no codigo!

GetPlot:
    li      $at, NUMCOLUNAS
    mult    $a1, $at
    nop
    nop
    mflo    $a1
    add     $a0, $a0, $a1
    la      $a1, VGAADDRESSINI          # endereco VGA
    nop
    nop
    or      $a0, $a0, $a1
    lb      $a2, 0($a0)
    jr      $ra



#########################
#    ReadChar           #
# $v0 = valor do char   #
#                       #
#########################


#endereco buffer1
readChar:
    la      $t0, Buffer0Teclado
    nop
    nop

#endereco buffer 2
    la      $t1, Buffer1Teclado
    nop
    nop

# carrega buffers e o shift
    addi    $t7, $zero, 0x12            # carrega o shift
    nop
    nop
    addi    $t8, $zero, 0xF0            # carrega o F0
    nop
    nop
    add     $t9, $zero, $zero           # shif precionado

    lw      $t6, 0($t0)
loopReadChar:
    lw      $t2, 0($t0)
    nop
    nop
    beq     $t2, $t6, atualizaBufferChar    # testa se o buffer foi modificado
    nop
    nop
    nop
    j       modificado

atualizaBufferChar:
    add     $t6, $t2, $zero
    nop
    nop
    nop
    j       loopReadChar

modificado:
    andi    $t4, $t2, 0xFF
    nop
    nop
    addi    $t5, $zero, 0x12
    nop
    nop
    beq     $t4, $t5, shiftindahouse

#testa se for F0
    andi    $t4, $t2, 0xFF
    nop
    nop
    beq     $t4, $t8, atualizaBufferChar

#testa se a tecla foi pressionada e solta
    andi    $t4, $t2, 0xFF00
    nop
    nop
    addi    $t5, $zero, 0xF000
    nop
    nop
    beq     $t4, $t5, continua          # tecla foi solta
    add     $t6, $t2, $zero
    j       loopReadChar

#testa se a tecla foi solta
continua:
    andi    $t4, $t2, 0xFF              # ultima tecla inserida
    nop
    nop
    beq     $t4, $t7, shiftindahouse    # se valor é shift

    addi    $t5, $zero, 1
    nop
    nop
    beq     $t9, $t5, enderecoShift

    sll     $t4, $t4, 2                 # mult 4
    la      $t5, inicioKdata
    nop
    nop
    add     $t4, $t4, $t5               # inicio endereco na memoria .kdata
    nop
    nop
    addi    $t4, $t4, 0x318             # final da string para o printChar sem shit
    nop
    nop
    lw      $t5, 0($t4)
    nop
    nop
    beq     $t5, $zero, atualizaBufferChar

    j       ReadCharEnd

enderecoShift:
    andi    $t4, $t2, 0xFF              # ultima tecla inserida
    # srl     $t4, $t4, 16
    sll     $t4, $t4, 2                 # mult 4
    nop
    nop
    la      $t5, inicioKdata
    nop
    nop
    add     $t4, $t4, $t5               # inicio endereco na memoria .kdata
    nop
    nop
    addi    $t4, $t4, 0x530             # final da string para o printChar com shift
    nop
    nop
    lw      $t5, 0($t4)
    nop
    nop
    beq     $t5, $zero, atualizaBufferChar

    j       ReadCharEnd

shiftindahouse:
    addi    $t9, $zero, 1               # //NOTE: Havia uma vírgula após addi. Não sei como o montador interpretava aquilo...
    j       atualizaBufferChar

ReadCharEnd:
    add     $v0, $zero, $t5             # coloca em v0 o valor em ascii da tecla
    jr      $ra

#########################
#    ReadInt            #
# $v0 = valor do inteiro#
#                       #
#########################

#iniciando variaveis
readInt:
    addi    $v0, $zero, 0
    addi    $t7, $zero, 0
    # addi    $s3, $s3, 1                 #### ?????

#endereco buffer1
    la      $t0, Buffer0Teclado
    nop
    nop

#endereco buffer 2
    la      $t1, Buffer1Teclado
    nop
    nop

#leitura inical do buffer
    lw      $t8, 0($t0)                 # buffer inicial
    lw      $t9, 0 ($t0)                # buffer inicial

loopReadInt:
    lw      $t2, 0($t0)
    nop
    nop
    beq     $t2, $t8, atualizaBuffer
    lw      $t3, 0($t1)
    #beq $t3, $t9, atualizaBuffer --------------- Nao sei se precisa desta linha

#testa se a tecla foi pressionada e solta
    andi    $t4, $t2, 0xFF00
    addi    $t5, $zero, 0xF000
    nop
    nop
    beq     $t4, $t5, continuaInt
    j       atualizaBuffer

continuaInt:
    andi    $t4, $t2, 0x000000FF

#verifica se os ultimos digitos sao F0
    addi    $t5, $zero, 0xF0
    nop
    nop
    beq     $t4, $t5, atualizaBuffer

#verifica se o enter foi pressionado
    addi    $t5, $zero, 0x5a
    nop
    nop
    beq     $t4, $t5, fimReadInt1       # pressionado o enter

#pega o codigo ascii baseado no scan code
    sll     $t4, $t4, 2                 # mult 4
    la      $t5,inicioKdata
    nop
    nop
    add     $t4, $t4, $t5               # inicio endereco na memoria .kdata
    addi    $t4, $t4, 0x318             # final da string para o printChar
    nop
    nop
    lw      $t5, 0($t4)                 # posicao na memoria

# testa se o valor esta entre 0x30 <= x <= 0x39
    addi    $t6, $zero, 0x2f            # inicio dos inteiros - 1
    nop
    nop
    slt     $t4, $t6, $t5
    beq     $t4, $zero, naoInteiro
    addi    $t6, $t6, 0xB               # final dos inteiros
    nop
    nop
    slt     $t4, $t5, $t6
    beq     $t4, $zero, naoInteiro

# retorna o valor inteiro para $v0
    andi    $t5, $t5, 0xF               # 0x31 = 1, so depende dos bits finais
    addi    $t4, $zero, 10
    nop
    nop
    mult    $v0, $t4                    # numero vezes 10 (unidade, dezena, centena...)
    nop
    nop
    mflo    $v0
    add     $v0, $v0, $t5

    j       atualizaBuffer

naoInteiro:
    addi    $t4, $zero, 0x2D
    nop
    nop
    beq     $t4, $t5, negativo

    j       atualizaBuffer

negativo:
    slt     $t7, $zero, $v0
    addi    $t6, $zero, 1
    nop
    nop
    beq     $t7, $t6, atualizaBuffer
    addi    $t7, $zero, 1               #1 para negativo

atualizaBuffer:
    add     $t8, $zero, $t2             # copia o buffer atual para variavel de buffer anterior
    add     $t9, $zero, $t3             # copia o buffer atual para variavel de buffer anterior

    j       loopReadInt

fimReadInt1:
    beq     $t7, $zero, fimReadInt2
    sub     $v0, $zero, $v0

fimReadInt2:
    add     $t8, $zero, $t2             # copia o buffer atual para variavel de buffer anterior
    add     $t9, $zero, $t3             # copia o buffer atual para variavel de buffer anterior

    jr      $ra                         # fim readInt

#########################
#    ReadString         #
# $a0 = end Inicio      #
# $a1 = tam Max String  #
#                       #
#########################

readString:
    add     $t6, $zero, $a0             # end inicial string
    sub     $t7, $a1, 1                 # tamanho maximo menos 1 (guarda para 0x00)
    sll     $t7, $t7, 2                 # tamanho maximo multiplicado por 4
    add     $t1, $zero, $zero           # contador de caracteres (de 4 em 4)
    move    $k0, $zero                  # contador de 4
    li      $v0, 0                      # flag de ultimo zero

#endereco buffer1
    la      $t0, Buffer0Teclado
    nop
    nop
#leitura inical do buffer
    lw      $t8, 0($t0)                 # buffer1 inicial

loopReadString:
    beq     $t7, $t1, fimReadString

    lw      $t2, 0($t0)
    nop
    nop
    nop
    beq     $t2, $t8, atualizaBufferString

#testa se a tecla foi pressionada e solta
    andi    $t4, $t2, 0xFF00
    addi    $t5, $zero, 0xF000
    nop
    nop
    beq     $t4, $t5, continuaString
    j       atualizaBufferString

continuaString:
    andi    $t4, $t2, 0x00FF

#verifica se os ultimos digitos sao F0
    addi    $t5, $zero, 0xF0
    nop
    nop
    nop
    beq     $t4, $t5, atualizaBufferString

#verifica se o enter foi pressionado
    addi    $t5, $zero, 0x5a
    nop
    nop
    nop
    beq     $t4, $t5, fimReadString #pressionado o enter

#pega o codigo ascii baseado no scan code
    sll     $t4, $t4, 2                 # mult 4
    # addi    $t4, $t4, 0x10000           # inicio endereco na memoria
    la      $t5, inicioKdata
    nop
    nop
    add     $t4, $t4, $t5               # inicio endereco na memoria .kdata
    nop
    nop
    addi    $t4, $t4, 0x318             # final da string para o printChar
    nop
    nop
    nop
    lw      $t5, 0($t4)                 # posicao na memoria
    nop
    nop
    nop
    beq     $t5, $zero, atualizaBufferString

VoltaZeroString:
    add     $t4, $t6, $t1               # endereco para escrita
    nop
    nop
    #sw $t5, 0($t4)         #guarda char valido
    #li $a3,4
    #j PPULA    Original 1 caractere por Word
    lw      $t9, 0($t4)                 # le o que tem no endereco
    # o que faz a falta do sllv !!!!
    li      $a2, 0
    nop
    nop
    beq     $k0, $a2, Jzero
    li      $a2, 1
    nop
    nop
    beq     $k0, $a2, Jum
    nop
    nop
    li      $a2, 2
    nop
    nop
    beq     $k0, $a2, Jdois

Jtres:
    la      $k1, 0x00FFFFFF
    nop
    nop
#    lui $k1,0x00FF
#    ori $k1,0xFFFF
    sll     $t5, $t5, 24
    li      $k0, 0
    li      $a3, 4
    j       Jsai
Jdois:
    la      $k1, 0xFF00FFFF
    nop
    nop
#    lui $k1,0xFF00
#    ori $k1,0xFFFF
    sll     $t5,$t5,16
    li      $k0,3
    li      $a3,0
    j       Jsai
Jum:
    la      $k1, 0xFFFF00FF
#    lui $k1,0xFFFF
#    ori $k1,0x00FF
    sll     $t5, $t5, 8
    li      $a3, 0
    li      $k0, 2
    j       Jsai
Jzero:
    la      $k1, 0xFFFFFF00
    nop
    nop
#    lui $k1,0xFFFF
#    ori $k1,0xFF00
    sll     $t5, $t5, 0
    li      $k0, 1
    li      $a3, 0

Jsai:
    and     $t9, $t9, $k1
    or      $t5, $t5, $t9


PPULA:
    sw      $t5, 0($t4)                 # guarda char valido

    add     $t1, $t1, $a3               # caractere inserido, atualiza contador

atualizaBufferString:
    add     $t8, $zero, $t2             # copia o buffer atual para variavel de buffer anterior
    beq     $v0, $zero, loopReadString
#    add $t1, $t1, 4
#    add $t4, $t6, $t1
#    sw $zero, 0($t4)
    jr      $ra

    #fim da string 0x00
fimReadString:
    li      $v0, 1                      # ultimo
    li      $t5, 0                      # zero
    j       VoltaZeroString

###########################################
#        MidiOut 31 (2015/1)              #
#  $a0 = pitch (0-127)                    #
#  $a1 = duration in milliseconds         #
#  $a2 = instrument (0-15)                #
#  $a3 = volume (0-127)                   #
###########################################


#################################################################################################
#
# Note Data           = 32 bits     |   1'b - Melody   |   4'b - Instrument   |   7'b - Volume   |   7'b - Pitch   |   1'b - End   |   1'b - Repeat   |   11'b - Duration   |
#
# Note Data (Syscall) = 32 bits     |   1'b - Melody   |   4'b - Instrument   |   7'b - Volume   |   7'b - Pitch   |   13'b - Duration   |
#
#################################################################################################


midiOut:
    la      $t0, NoteData
    add     $t1, $zero, $zero

    # Melody = 0

    # Definicao do Instrumento
    andi    $t2, $a2, 0x0000000F
    sll     $t2, $t2, 27
    or      $t1, $t1, $t2

    # Definicao do Volume
    andi    $t2, $a3, 0x0000007F
    sll     $t2, $t2, 20
    or      $t1, $t1, $t2

    # Definicao do Pitch
    andi    $t2, $a0, 0x0000007F
    sll     $t2, $t2, 13
    or      $t1, $t1, $t2

    #Definicao da Duracao
    andi    $t2, $a1, 0x00001FFF
    or      $t1, $t1, $t2

    # Guarda a definição da duração da nota na Word 1
    j       SintMidOut
    nop
SintMidOut:
    sw      $t1, 0($t0)

    # Verifica a subida do clock AUD_DACLRCK para o sintetizador receber as definicoes
    la      $t2, NoteClock
Check_AUD_DACLRCK:
    lw      $t3, 0($t2)
    nop
    nop
    beq     $t3, $zero, Check_AUD_DACLRCK
    nop
    jr      $ra
    nop

###########################################
#        MidiOut 33 (2015/1)              #
#  $a0 = pitch (0-127)                    #
#  $a1 = duration in milliseconds         #
#  $a2 = instrument (0-127)               #
#  $a3 = volume (0-127)                   #
###########################################

#################################################################################################
#
# Note Data             = 32 bits     |   1'b - Melody   |   4'b - Instrument   |   7'b - Volume   |   7'b - Pitch   |   1'b - End   |   1'b - Repeat   |   8'b - Duration   |
#
# Note Data (Syscall)   = 32 bits     |   1'b - Melody   |   4'b - Instrument   |   7'b - Volume   |   7'b - Pitch   |   13'b - Duration   |
#
#################################################################################################

midiOutSync:

    la      $t0, NoteData
    add     $t1, $zero, $zero

    # Melody = 1
    ori     $t1, $t1, 0x80000000

    # Definicao do Instrumento
    andi    $t2, $a2, 0x0000000F
    sll     $t2, $t2, 27
    or      $t1, $t1, $t2

    # Definicao do Volume
    andi    $t2, $a3, 0x0000007F
    sll     $t2, $t2, 20
    or      $t1, $t1, $t2

    # Definicao do Pitch
    andi    $t2, $a0, 0x0000007F
    sll     $t2, $t2, 13
    or      $t1, $t1, $t2

    #Definicao da Duracao
    andi    $t2, $a1, 0x00001FFF
    or      $t1, $t1, $t2

    # Guarda a definição da duração da nota na Word 1
    j       SintMidOutSync
    nop

SintMidOutSync:
    sw      $t1, 0($t0)

    # Verifica a subida do clock AUD_DACLRCK para o sintetizador receber as definicoes
    la      $t2, NoteClock
    la      $t4, NoteMelody

Check_AUD_DACLRCKSync:
    lw      $t3, 0($t2)
    nop
    nop
    beq     $t3, $zero, Check_AUD_DACLRCKSync
Melody:
    lw      $t5, 0($t4)
    nop
    nop
    bne     $t5, $zero, Melody
    nop
    jr      $ra
    nop

#################################
#    InKey                      #
# $v0 = primeira tecla          #
# $v1 = ultima tecla            #
#                               #
#################################

#iniciando variaveis
inKey:
    addi    $v0, $zero, 0
    addi    $v1, $zero, 0

#endereco buffer1
    la      $t0,Buffer0Teclado
    nop
    nop

#endereco buffer 2
    la      $t1, Buffer1Teclado
    nop
    nop

#leitura inical do buffer
    lw      $t8, 0($t0)                 # buffer inicial
    lw      $t9, 0($t1)                 # buffer inicial

loopInKey:
    lw      $t2, 0($t0)
#    beq $t2, $t8, atualizaBufferInKey
    lw      $t3, 0($t1)
#
#    andi $t4, $t2, 0xFF00FF00 # queremos ver se duas teclas foram soltas
#    addi $t5, $zero, 0xF000F000
#    beq $t4, $t5, continuaInKey
#
#atualizaBufferInKey:
#    add $t8, $zero, $t2    #copia o buffer atual para variavel de buffer anterior
#    add $t9, $zero, $t3    #copia o buffer atual para variavel de buffer anterior
#
#    j loopInKey
#
#continuaInKey:
    andi    $t4, $t2, 0xFF0000          # pseudo
    nop
    nop
    srl     $t4, $t4, 16
    sll     $t4, $t4, 2                 # mult 4
    la      $t5, inicioKdata
    nop
    nop
    add     $t4, $t4, $t5               # inicio endereco na memoria
    nop
    nop
    addi    $t4, $t4, 0x318             # final da string para o printChar
    nop
    nop
    lw      $t5, 0($t4)                 # posicao na memoria
    add     $v0, $zero, $t5

    andi    $t4, $t2, 0xFF
    nop
    nop
    sll     $t4, $t4, 2                 # mult 4
    la      $t5, inicioKdata
    nop
    nop
    add     $t4, $t4, $t5               # inicio endereco na memoria
    nop
    nop
    addi    $t4, $t4, 0x318             # final da string para o printChar
    nop
    nop
    lw      $t5, 0($t4)                 # posicao na memoria
    add     $v1, $zero, $t5

    jr      $ra


#################################
#    CLS                        #
#  Clear Screen                 #
#                               #
#################################

CLS:
    la      $t6,VGAADDRESSINI           # Memoria VGA
    nop
    nop
    la      $t2,VGAADDRESSFIM
    nop
    nop

    andi    $a0, $a0, 0x00FF
    sll     $t0, $a0, 8
    or      $t0, $t0, $a0
    sll     $t0, $t0, 8
    or      $t0, $t0, $a0
    sll     $t0, $t0, 8
    or      $a0, $t0, $a0

Fort3:
    beq     $t2, $t6, Endt3
    sw      $a0, 0($t6)
    addi    $t6, $t6, 4
    nop
    j       Fort3
Endt3:
    jr      $ra


#################################
#    Pop event                  #
#  $v0 = sucesso ? 1 : 0        #
#  $v1 = evento                 #
#################################

popEvent:
    addi    $sp, $sp, -12
    sw      $a0, 0($sp)
    sw      $s0, 4($sp)
    sw      $ra, 8($sp)

    # verifica se ha eventos na fila comparando os ponteiros de inicio e fim
    la      $s0, eventQueueBeginPtr
    nop
    nop
    lw      $k0, 0($s0)
    la      $k1, eventQueueEndPtr
    nop
    nop
    lw      $k1, 0($k1)
    li      $v0, 0
    nop
    beq     $k0, $k1, popEventEnd

    # tira o evento da fila e coloca em $v1
    move    $a0, $k0
    jal     eventQueueIncrementPointer
    sw      $v0, 0($s0)
    li      $v0, 1
    lw      $v1, 0($k0)

popEventEnd:
    lw      $ra, 8($sp)
    lw      $s0, 4($sp)
    lw      $a0, 0($sp)
    addi    $sp, $sp, 12
    jr      $ra




#################################
# printFloat                    #
# coloca bo FloatBuffer         #
# a string do float em $f12     #
#################################
#Primeira etapa: obter mantissa em base 10 e expoente
printFloat:
    li      $t0, 0x7F800000
    srl     $t0, $t0, 16
    mfc1    $t1, $f12
    srl     $t1, $t1, 16
    sub     $t2, $t0, $t1
    beqz    $t2, INFINITYPLUS

    li      $t0, 0xFF800000
    srl     $t0, $t0, 16
    mfc1    $t1, $f12
    srl     $t1, $t1, 16
    sub     $t2, $t0, $t1
    beqz    $t2, INFINITYMINUS

    mfc1    $t0, $f12
    li      $t1, 0xFF
    sll     $t1, $t1, 23

    and     $t2, $t0, $t1

    bnez    $t2, continue
    li      $s5, 0x00000030             #"0\0"
    sw      $s5, 0($s6)
    jr      $ra                         ### return printFloat

continue:
    addi    $sp, $sp, -4
    sw      $ra, 0($sp)
    jal     OBTAIN_MANTISSA_EXP
    lw      $ra, 0($sp)
    addi    $sp, $sp, 4

    seq     $t9, $s2, 128

    li      $t0, 1
    beq     $s1, $t0, seNegativo

    li      $t0, 0x3649539C             #3e-6
    mtc1    $t0, $f15
sePositivo:
    add.s   $f10, $f10, $f15
    j       continue100
seNegativo:
    sub.s   $f10, $f10, $f15

continue100:
    neg     $t8,$t8
    and     $t6, $t9, $t8
    bnez    $t6, NAN

    ## Salvar espaco ou sinal primeiro
    seq     $t0, $s1, 1
    move    $s7, $sp                    # para recuperar sp depois
    beqz    $t0, CONTINUE1
    addiu   $t0, $zero, '-'             # Save Signal ASCII
    or      $s5, $t0, $zero             # first byte saved
    j       CONTINUE2

CONTINUE1:
    addiu   $t0, $zero, '+'
    or      $s5, $t0, $zero             #first byte saved
#------------
# Imprimir numero antes do ponto

#Verificar se a mantissa esta normalizada
CONTINUE2:
    seq     $t8, $s1, 1
    beqz    $t8, normalizaPraCima
    li      $t0, 0xBF800000             # -1
    mtc1    $t0, $f25                   # -1.0
    mul.s   $f10, $f10, $f25            # turns it positive

normalizaPraCima:
    li      $t0, 0x3F800000             # 1
    mtc1    $t0, $f23
    li      $t0, 0x41200000             # 10
    mtc1    $t0, $f24
    c.lt.s  $f10, $f23                  # vê se mantissa é menor que 1
    bc1f    NOTZERO
    mul.s   $f10, $f24, $f10
    addi    $s4, $s4, -1                # decrementa o expoente por 1
    j       normalizaPraCima

normalizaPraBaixo:
    li      $t0, 0x41200000             # 10
    mtc1    $t0, $f24
    c.lt.s  $f10, $f24                  # vê se mantissa é menor que 1
    bc1t    NOTZERO
    div.s   $f10, $f10, $f24
    addi    $s4, $s4, +1                # decrementa o expoente por 1
    j       normalizaPraBaixo

NOTZERO:
    li      $t0, 0x41200000             # 10
    mtc1    $t0, $f14
    c.lt.s  $f10, $f14
    bc1t    CONTINUE3
    div.s   $f10, $f10, $f14            # dividir por 10, pois f10 era maior que 10
    addiu   $s4, $s4, 1                 # adicionar 1 ao expoente na base 10 depois desta divisao

CONTINUE3:
    cvt.w.s $f0, $f10
    mfc1    $t0, $f0
    cvt.s.w $f25, $f0
    c.le.s  $f25, $f10
    bc1t    a1
    addi    $t0, $t0, -1
a1:
    addi    $t0, $t0, '0'               # Inicio dos caracteres de numeros, 48 = '0'
    sll     $t0, $t0, 8

    or      $s5, $s5, $t0               # second byte saved
    srl     $t0, $t0, 8

    li      $t1, '.'                    # o ponto!!
    sll     $t1, $t1, 16
    or      $s5, $s5, $t1               # third byte save
    srl     $t1, $t1, 16

    sub     $t0, $t0, '0'
    mtc1    $t0, $f17                   # Pega valor truncado do coprocessador 1
    cvt.s.w $f17, $f17                  # torna valor em float
    sub.s   $f10, $f10, $f17            # Subtrai do valor da mantissa, deixando o numero como 0.ABCDEF
    mul.s   $f10, $f10, $f14            # numero fica A.BCDEFX

    cvt.w.s $f0, $f10
    mfc1    $t0, $f0                    # Salva A em t0
    cvt.s.w $f25, $f0
    c.le.s  $f25, $f10
    bc1t    a2
    addi    $t0, $t0, -1
a2:
    addi    $t0, $t0, '0'
    sll     $t0, $t0, 24
    or      $s5, $s5, $t0               # fourth byte save
    srl     $t0, $t0, 24
    sw      $s5, 0($s6)                 # four bytes group saved

    sub     $t0, $t0, '0'
    mtc1    $t0, $f17                   # Pega valor truncado do coprocessador 1
    cvt.s.w $f17, $f17                  # torna valor em float
    sub.s   $f10, $f10, $f17            # Subtrai do valor da mantissa, deixando o numero como 0.BCDEFX
    mul.s   $f10, $f10, $f14            # numero fica B.CDEFXX

    cvt.w.s $f0, $f10
    mfc1    $t0, $f0                    # Salva B em t0
    cvt.s.w $f25, $f0
    c.le.s  $f25, $f10
    bc1t    a3
    addi    $t0, $t0, -1
a3:
    addi    $t0, $t0, '0'
    or      $s5, $t0, $zero             # first byte saved

    sub     $t0, $t0, '0'
    mtc1    $t0, $f17                   # Pega valor truncado do coprocessador 1
    cvt.s.w $f17, $f17                  # torna valor em float
    sub.s   $f10, $f10, $f17            # Subtrai do valor da mantissa, deixando o numero como 0.CDEFXX
    mul.s   $f10, $f10, $f14            # numero fica C.DEFXXX


    cvt.w.s $f0, $f10
    mfc1    $t0, $f0                    # Salva C em t0
    cvt.s.w $f25, $f0
    c.le.s  $f25, $f10
    bc1t    a4
    addi    $t0, $t0, -1
a4:
    addi    $t0, $t0, '0'
    sll     $t0, $t0, 8
    or      $s5, $s5, $t0               # second byte saved
    srl     $t0, $t0, 8

    sub     $t0, $t0, '0'
    mtc1    $t0, $f17                   # Pega valor truncado do coprocessador 1
    cvt.s.w $f17, $f17                  # torna valor em float
    sub.s   $f10, $f10, $f17            # Subtrai do valor da mantissa, deixando o numero como 0.DEFXXX
    mul.s   $f10, $f10, $f14            # numero fica D.EFXXXX

    cvt.w.s $f0, $f10
    mfc1    $t0, $f0                    # Salva D em t0
    cvt.s.w $f25, $f0
    c.le.s  $f25, $f10
    bc1t    a5
    addi    $t0, $t0, -1
a5:
    addi    $t0, $t0, '0'
    sll     $t0, $t0, 16
    or      $s5, $s5, $t0               # third byte saved
    srl     $t0, $t0, 16

    sub     $t0, $t0, '0'
    mtc1    $t0, $f17                   # Pega valor truncado do coprocessador 1
    cvt.s.w $f17, $f17                  # torna valor em float
    sub.s   $f10, $f10, $f17            # Subtrai do valor da mantissa, deixando o numero como 0.EFXXXX
    mul.s   $f10, $f10, $f14            # numero fica E.FXXXX

    cvt.w.s $f0, $f10
    mfc1    $t0, $f0                    # Salva E em t0
    cvt.s.w $f25, $f0
    c.le.s  $f25, $f10
    bc1t    a6
    addi    $t0, $t0, -1
a6:
    addi    $t0, $t0, '0'
    sll     $t0, $t0, 24
    or      $s5, $s5, $t0               # fourth byte saved
    srl     $t0, $t0, 24
    sw      $s5, 4($s6)                 # more four bytes saved

    sub     $t0, $t0, '0'
    mtc1    $t0, $f17                   # Pega valor truncado do coprocessador 1
    cvt.s.w $f17, $f17                  # torna valor em float
    sub.s   $f10, $f10, $f17            # Subtrai do valor da mantissa, deixando o nÃºmero como 0.FXXXXX
    mul.s   $f10, $f10, $f14            # numero fica F.XXXXX

    cvt.w.s $f0, $f10
    mfc1    $t0, $f0                    # Salva F em t0
    cvt.s.w $f25, $f0
    c.le.s  $f25, $f10
    bc1t    a7
    addi    $t0, $t0, -1
a7:
    addi    $t0, $t0, '0'
    or      $s5, $t0, $zero
    #sll $s5, $t0, 24 # first byte saved

OUT_LOOP_DEPOIS_DA_VIRGULA:
    li      $t0, 'E'
    sll     $t0, $t0, 8
    or      $s5, $s5, $t0               # second byte saved
    srl     $t0, $t0, 8

    move    $t8, $sp
    addi    $t8, $t8, 9
    li      $t1, 10
    sgt     $t9, $s4, $0                # O expoente e maior que 0? Se sim, t9 = 1
    bnez    $t9, EXPOENTE_CHAR1
    li      $t1, '-'
    sll     $t1, $t1, 16
    or      $s5, $s5, $t1               # third byte saved
    srl     $t1, $t1, 16

    j       EXPOENTE_CHAR

EXPOENTE_CHAR1:
    li      $t1, '+'
    sll     $t1, $t1, 16
    or      $s5, $s5, $t1               # third byte saved
    srl     $t1, $t1, 16

EXPOENTE_CHAR:
    li      $t1, 10
    slti    $t5, $s4, 0
    beqz    $t5, EXPONENT
    not     $s4, $s4
    addi    $s4, $s4, 1

EXPONENT:
    div     $s4, $t1
    mflo    $t0                         # exp10 / 10


    addiu   $t0, $t0, '0'
    sll     $t0, $t0, 24
    or      $s5, $s5, $t0               # fourth byte saved
    srl     $t0, $t0, 24
    sw      $s5, 8($s6)

    mfhi    $t0

    #li $v0, 1
    #move $a0, $t0
    #syscall

    addiu   $t0, $t0, '0'
    or      $s5, $t0, $zero             # first byte saved

    li      $t0, '\n'
    sll     $t0, $t0, 8
    or      $s5, $s5, $t0               # second byte saved

    li      $t0, '\0'
    sll     $t0, $t0, 16
    or      $s5, $s5, $t0               # third byte saved
    sw      $s5, 12($s6)

    j       endPrintFloat

INFINITYPLUS:
    li      $s5, 0x666E4920             # " Inf"
    sw      $s5, 0($s6)
    li      $s5, 0x74696E69             # "init"
    sw      $s5, 4($s6)
    li      $s5, 0x00000079             # "y\0"
    sw      $s5, 8($s6)

    jr      $ra

INFINITYMINUS:
    li      $s5, 0x666E492D             # " Inf"
    sw      $s5, 0($s6)
    li      $s5, 0x74696E69             # "init"
    sw      $s5, 4($s6)
    li      $s5, 0x00000079             # "y\0"
    sw      $s5, 8($s6)

    jr      $ra

NAN:
    li      $s5, 0x004E614E             # "\NaN\0"
    sw      $s5, 0($s6)
    jr      $ra

    j       endPrintFloat
endPrintFloat:
    jr      $ra


#Reads IEEE 754 Number in $f12
#Obtains mantissa and saves in $f10 and expoent in $s4
#Read number from coprocessor 1 to s0
OBTAIN_MANTISSA_EXP:
    mfc1    $s0, $f12                   # Take number in IEEE 754
# -------------------------
# Save sign of IEEE 754 in s1
    srl     $s1, $s0, 31                # t1 = sign
#---------------------------
# Save exponent of IEEE 754 in s2
    srl     $s2, $s0, 23
    andi    $s2, $s2, 0xFF
    addi    $s2, $s2, -127
# exp = ((x >> 23) & 0xff) - 127

#-------------------------------------------
# Mantissa, save in s3
    li      $t0, 1
    sll     $t0, $t0, 23
    addi    $t0, $t0, -1                # t0 = (1<<23) - 1
    and     $s3, $t0, $s0               # man = x & ((1<<23) - 1)

    addi    $t0, $t0, 1
    or      $s3, $t0, $s3               # man |= (1 << 23)
#---------------------------------------
#Floating point number with exponent = 0, save in f10
    addi    $t0, $s2, 127
    sll     $t0, $t0, 23
    sub     $t1, $s0, $t0               # x - ((exp+127) << 23)

    li      $t0, 0x7f
    sll     $t0, $t0, 23
    or      $t1, $t1, $t0               # s4 |= 0x7f << 23
    mtc1    $t1, $f10                   # mantissa sem expoente incompleta = f10

# ------------------------

# Calculate n ln(2) e salvar expoente na base 10 em s4
    mtc1    $s2, $f30
    cvt.s.w $f30, $f30                  # float(exponent)
    li      $t0, 0x3F317218             # ln(2)
    mtc1    $t0, $f1                    # ln(2)
    mtc1    $s2, $f2                    # take exponent
    cvt.s.w $f2, $f2                    # Convert to single precision
    mul.s   $f2, $f2, $f1               # f2 = n ln(2)

    #Obtain floor (f) e salvar expoente na base 10 em s4
    li      $t0, 0x3E9A209B             # log(2)
    mtc1    $t0, $f1

    mtc1    $s2, $f2                    # take exponent
    cvt.s.w $f2, $f2                    # Convert to single precision
    mul.s   $f2, $f1, $f2               # f
    li      $t0, 0xBF800000             #-1
    mtc1    $t0, $f5                    # -1
    #Calcular floor(f)
    #Implementar ceil
    mtc1    $zero,$f23                  # lwc1 $f23, 0x0
    c.lt.s  $f2, $f23                   # checa se f < 0
    mov.s   $f4, $f2

    bc1t    menorQueZero
    li      $t0, 0xBF800000
maiorQueZero:
    cvt.w.s $f4, $f4                    # se x > 0, ceil(f) = trunca(x)
    j       salvaFloor

menorQueZero:
    li      $t0, 0xBF79999A             # se x < 0, floor(f) = trunca(x-0.975)
    mtc1    $t0, $f23                   # f23 = 0.975
    add.s   $f4, $f4, $f23
    cvt.w.s $f4, $f4

salvaFloor:
    mfc1    $s4, $f4                    # save ceil(f)
    cvt.s.w $f4, $f4

#Obter mantissa na base 10
    li      $t0, 0x40135D8E             # ln(10)
    mtc1    $t0, $f1

    mul.s   $f4, $f4, $f1               # floor(f)*ln(10)

    li      $t0, 0x3F317218             # ln(2)
    mtc1    $t0, $f1                    # ln(2)
    mtc1    $s2, $f2                    # take exponent
    cvt.s.w $f2, $f2                    # Convert to single precision
    mul.s   $f3, $f2, $f1               # n ln(2)
    sub.s   $f30, $f3, $f4              # f30 = n ln(2) - floor(f)*ln(10)

    # cuidado com o $ra, tem de salvar

    move    $s7, $ra
    jal     EXP                         # 2 ^ exp2 / 10 ^ exp10
    move    $ra, $s7
    mul.s   $f10, $f31, $f10            # f10 = MANTISSA NA BASE 10

END_OBTAIN_MANTISSA:
    jr      $ra

#######################-----Calcula Exponencial-----#######################
#Argumento esta em x = $f30, saida em exp(x) = $f31
# Load number of iterations max
EXP:
    li      $t1, 30
# -----------------------------
    mtc1    $zero,$f1                   # lwc1 $f1, 0x0#f1 = 0
    mov.s   $f2, $f30                   # f2 = x
    li      $t0, 0x3F800000             # 1
    mtc1    $t0, $f31                   # 1.0

    add.s   $f31, $f31, $f30            # exp($f30) = 1 + x
    move    $t0, $zero                  # index = 0
    addi    $t0, $t0, 1                 # index = 1
    mtc1    $t0, $f5                    # f5 = t0
    cvt.s.w $f5, $f5                    # f5 = float(t0) = factorial(index)

# Index, conversion from int to float,
FOR:
    addi    $t0, $t0, 1                 # i = i + 1
    mtc1    $t0, $f3                    # f2 = index
    cvt.s.w $f3, $f3                    # f3 = float(index)
# ---------------------------------
# Factorial
    mul.s   $f5, $f5, $f3               # factorial(index)
# ---------------------------------

#Multiply x^(n-1) by x and then obtain new term
    mul.s   $f2, $f2, $f30              # f2 = x^n
    div.s   $f4, $f2, $f5               # f4 = x^n / factorial(index)
# --------------------------------

#Add new term
    add.s   $f31, $f31, $f4
# -----------------

#Check if it is time to end loop
    sle     $t2, $t0, $t1
    beqz    $t2, END
    j       FOR
#------------------
END:
    jr      $ra
#######################---End Calcula Exponencial---#######################


#########################
# readFloat        #
# para $f0        #
#########################
readFloat:
    la      $a0, FloatBuffer            # buffer caracteres
    li      $a1,32                      # numero maximo de digitos
    li      $v0,8                       # read string
    jal     readString                  # syscall

    move    $s1, $a0

    move    $s3, $0                     # $s3 = contador de caracteres na parte fracionaria
    move    $s4, $0                     # $s4 = flag para sinal do numero
    li      $t4, 0                      # $t4 = total inteiro (int)
    li      $t5, 0                      # $t5 = total float
    li      $t1, 10                     # $t1 = valor a ser dividido
    mtc1    $t1, $f7                    # convertendo ele para float
    cvt.s.w $f7, $f7
    mtc1    $0, $f0                     # zerando o registrador $f0 que eh o resultado final

    li      $t0, 0x0                    # vou salvando os valores parciais aqui
    mtc1    $t0, $f1

    li      $t0, 0x41200000             # valor 10 fixo
    mtc1    $t0, $f2

    li      $t0, 0x41200000             # multiplos de 10
    mtc1    $t0, $f3

    li      $t0, 0x0                    # vou salvando os valores parciais aqui
    mtc1    $t0, $f13

#define se eh positivo ou nao
    move    $t9, $s1
    move    $t8, $ra
    jal     loadbytet0
    move    $ra, $t8
    beq     $t0, '+', floop1
    bne     $t0, '-', floopE            # '-' = 45 Se for negativo prossegue, se nao ir para loop1
    li      $s4, 1                      # $s4 sinalizado 1 como negativo
floop1:
    addi    $s1, $s1, 1                 # avanca o ponteiro da pilha

floopE:
    move     $s7, $s1
floop2:
    move    $t9, $s7
    move    $t8, $ra
    jal     loadbytet0
    move    $ra, $t8
    beq     $t0, 0, floop3              #'\0' = 0 quando elemento atual da pilha for 0 pule para loop1
    beq     $t0, 46, floop3             # '.' = 46 quanto elemento atual da pilha for . ou seja acabou os numeros inteiros va para loop1
    beq     $t0, 10, floop3
    beq     $t0, 'E', floop3
    beq     $t0, 'e', floop3
    addi    $t0, $t0, -48               # subtrai-se 48 para encontrar o numero inteiro de 0 a 9 segundo a tabela ascii

#    slti $at,$t0,0       # $t0<0 ? 1:0
#    bne $at,$zero, isNaN    # Erro de digitacao
#    slti $at,$t0,10      # $t0<10 ? 1:0
#    bne $at,$zero, isNum    # correto
#isNaN:  lui $at,0xFFFF
#    ori $at,$at,0xFFFF
#    mtc1 $at,$f0
#    j backReadFloat

isNum:
    addiu   $s7, $s7, 1
    j       floop2

floop3:
    sub     $t0, $s7, $s1               # numero total de casas decimais da parte inteira
    addi    $t0, $t0, -1
    move    $t7, $t0
    li      $s2, 0                      # make sure doesn't use part of E algorithm
    move    $t8, $ra
    jal     n10                         # 10**n is salved in f31 after this
    move    $ra, $t8
    mov.s   $f5, $f31

#loop para calcular total da parte inteira

loop1:                                  # le o numero da pilha
    move    $t9, $s1
    move    $t8, $ra
    jal     loadbytet0
    move    $ra, $t8
    beq     $t0, 0, final               #'\0' = 0 quando elemento atual da pilha for 0 pule para end1p1
    beq     $t0, 46, endlp2             # '.' = 46 quanto elemento atual da pilha for . ou seja acabou os numeros inteiros va para end1p2
    beq     $t0, 10, final              # \n
    beq     $t0, 'E', stepE1
    beq     $t0, 'e', stepE1
    addi    $t0, $t0, -48               # subtrai-se 48 para encontrar o numero inteiro de 0 a 9 segundo a tabela ascii

    mtc1    $t0, $f7                    # passes read number from stack to c1
    cvt.s.w $f7, $f7                    # converts it to floating point number
    mul.s   $f3, $f5, $f7               # contains 10**n * (char)
    div.s   $f5, $f5, $f2               # makes 10**(n-1)
    add.s   $f1, $f1, $f3               # adding, adding...

    addi    $s1, $s1, 1                 # avanca o ponteiro da pilha

    j       loop1                       # pula para loop1

#loop para calcular total da parte fracionaria
endlp2:
    addi    $s1, $s1, 1                 # avanca o ponteiro da pilha

    li      $t0, 0x41200000
    mtc1    $t0, $f5
    li      $t0, 0x3F800000
    mtc1    $t0, $f3

loop2:
    move    $t9, $s1
    move    $t8, $ra
    jal     loadbytet0
    move    $ra, $t8                    # le $t0
    beq     $t0, 'e', stepE1            # '\0' = 0 quando elemento atual da pilha for 0 pule para end1p1
    beq     $t0, 'E', stepE1
    beq     $t0, '\0', final
    beq     $t0, '\n', final
    addi    $t0, $t0, -48               # subtrai-se 48 para encontrar o numero inteiro de 0 a 9 segundo a tabela ascii

    mul.s   $f3, $f2, $f3               # multiples of 10
    mtc1    $t0, $f7                    # take number to c1
    cvt.s.w $f7, $f7                    # convert to floating point
    div.s   $f11, $f7, $f3              # a part of fractionary number
    add.s   $f1, $f1, $f11

    addi    $s3, $s3, 1                 # soma 1 ao #s3, contador de caracteres da parte fracionaria
    addi    $s1, $s1, 1                 # avanca o ponteiro da pilha

    j       loop2                       # volta para loop2

stepE1:
    addi    $s1, $s1, 1
    #define se e positivo ou nao
    move    $t9, $s1
    move    $t8, $ra
    jal     loadbytet0
    move    $ra, $t8                    # $t0 = elemento atual da pilha
    beq     $t0, '+', stepE2
    bne     $t0, '-', stepEE            # '-' = 45 Se for negativo prossegue, se não ir para loop1
    li      $s2, 1                      # $s2 sinalizado 1 como negativo

stepE2:
    addi    $s1, $s1, 1                 # avanca o ponteiro da pilha

stepEE:
    move    $s7, $s1
stepE3:
    move    $t9, $s7
    move    $t8, $ra
    jal     loadbytet0
    move    $ra, $t8                    # le o numero da pilha
    beq     $t0, 0, stepE4              # '\0' = 0 quando elemento atual da pilha for 0 pule para loop1
    beq     $t0, 46, stepE4             # '.' = 46 quanto elemento atual da pilha for . ou seja acabou os numeros inteiros va para loop1
    beq     $t0, 10, stepE4             # \n
    addi    $t0, $t0, -48               # subtrai-se 48 para encontrar o numero inteiro de 0 a 9 segundo a tabela ascii
    addiu   $s7, $s7, 1
    j       stepE3

stepE4:
    sub     $t0, $s7, $s1               # numero total de casas decimais depois de E
    addi    $t0, $t0, -1
    mtc1    $t0, $f30
    move    $t7, $t0
    move    $s5, $s2
    li      $s2, 0                      # estava tendo problema com s2...
    move    $t8, $ra
    jal     n10                         # 10**n is salved in f31 after this
    move    $ra, $t8
    move    $s2, $s5                    # retorna s2 ao valor original
    mov.s   $f5, $f31

#loop para calcular total da parte inteira depois de E, que eh o que tem soh neste caso
    li      $t0, 0x41200000
    mtc1    $t0, $f5
stepE5:
    move    $t9, $s1
    move    $t8, $ra
    jal     loadbytet0
    move    $ra, $t8                    # le o numero da pilha
    beq     $t0, 0, final               # '\0' = 0 quando elemento atual da pilha for 0 pule para end1p1
    beq     $t0, 10, final              # '\n'
    addi    $t0, $t0, -48               # subtrai-se 48 para encontrar o numero inteiro de 0 a 9 segundo a tabela ascii

    mtc1    $t0, $f7                    # passes read number from stack to c1
    cvt.s.w $f7, $f7                    # converts it to floating point number
    mul.s   $f3, $f5, $f7               # contains 10**n * (char)
    div.s   $f5, $f5, $f2               # makes 10**(n-1)
    add.s   $f13, $f13, $f3             # adding, adding...

    addi    $s1, $s1, 1                 # avanca o ponteiro da pilha

    j       stepE5                      # pula para loop1

final:
    mtc1    $s4, $f10                   # s4 is signal, then f10 will be signal
    cvt.s.w $f10, $f10
    bnez    $s4, sign                   # change signal if s4 != 0
        #Exponente
go_on1:
    mtc1    $s2, $f14                   # s2 is signal of exponent, then f10 will be signal
    cvt.s.w $f14, $f14
    bnez    $s2, signE                  # change signal if s4 != 0

go_on2:
    cvt.w.s $f13, $f13
    mfc1    $t7, $f13
    cvt.w.s $f13, $f13
    move    $t8, $ra
    jal     n10
    move    $ra, $t8
    mul.s   $f1, $f1, $f31


print:
    mov.s   $f0, $f1 #mov.s $f12, $f1
    #li $v0, 2
    #syscall
    j       backReadFloat

    # finalizacao do programa syscall 10
    #li $v0, 10
    #syscall

sign:
    li      $t0, 0xBF800000 #-1
    mtc1    $t0, $f10
    mul.s   $f1, $f1, $f10
    j       go_on1

signE:
    j       go_on2                      # isso nao e mais necessario... so volte

#######################-----------------------------#######################
#######################-----Calcula 10**n-----#######################
#######################-----------------------------#######################

n10:
    addi    $sp, $sp, -20               # Argumento esta em t7 = n, saida em 10**n = $f31#Argumento esta em t7 = n, saida em 10**n = $f31
    sw      $ra, 0($sp)
    sw      $t0, 4($sp)
    sw      $t7, 8($sp)
    swc1    $f0, 12($sp)
    swc1    $f15, 16($sp)

    li      $t0, 0x41200000             # 10
    mtc1    $t0, $f0
    li      $t0, 0x3F800000             # 1
    mtc1    $t0, $f31
    bnez    $s2, multiply_neg

multiply_pos:
    beqz    $t7, END2                   # a0 e o valor de n, vai subtraindo ate chegar em 0 ai vai para fim
    mul.s   $f31, $f31, $f0
    addi    $t7, $t7, -1
    j       multiply_pos

multiply_neg:
    beqz    $t7, invert                 # a0 e o valor de n, vai subtraindo ate chegar em 0 ai vai para fim
    mul.s   $f31, $f31, $f0
    addi    $t7, $t7, -1
    j       multiply_neg

invert:
    li      $t0, 0x3F800000             # 1
    mtc1    $t0, $f15

    div.s   $f31, $f15, $f31            # 1 / 10 ** n

END2:
    lw      $ra, 0($sp)
    lw      $t0, 4($sp)
    lw      $t7, 8($sp)
    lwc1    $f0, 12($sp)
    lwc1    $f15, 16($sp)
    addi    $sp, $sp, 20
    jr      $ra

#######################-----------------------------#######################
#######################---End Calcula Exponencial---#######################
#######################-----------------------------#######################

loadbytet0:
    addi    $sp, $sp, -8
    sw      $11, 0($sp)
    sw      $12, 4($sp)

    #divide por 4, pega resto
    li      $11, 4
    div     $t9, $11
    mfhi    $11
    sub     $t0, $t9, $11               # este endereco em a0 agora esta alinhado
    lw      $t0, 0($t0)
    li      $12, 0
    beq     $12, $11, lb3
    li      $12, 1
    beq     $12, $11, lb2
    li      $12, 2
    beq     $12, $11, lb1
    li      $12, 3
    beq     $12, $11, lb0

#carrega valor correto para cada caso
lb0:
    srl     $t0, $t0, 24
    andi    $t0, 0xFF
    j       endlb
lb1:
    srl     $t0, $t0, 16
    andi    $t0, 0xFF
    j       endlb
lb2:
    srl     $t0, $t0, 8
    andi    $t0, 0xFF
    j       endlb
lb3:
    andi    $t0, 0xFF
    j       endlb
endlb:
    lw      $11, 0($sp)
    lw      $12, 4($sp)
    addi    $sp, $sp, 8
    jr      $ra


############################################
#  SD Card Read                            #                                    //TODO: Colocar nop's para garantir o funcionamento com o pipeline.
#  $a0    =    Origem Addr                 #                                    //TODO: Implementar identificação de falha na leitura do cartão.
#  $a1    =    Destino Addr                #
#  $a2    =    Quantidade de Bytes         #                                    //NOTE: $a2 deve ser uma quantidade de bytes alinhada em words
#  $v0    =    Sucesso? 0 : 1              #
############################################

sdRead:
    la      $s0, SD_INTERFACE_ADDR
    la      $s1, SD_INTERFACE_CTRL
    la      $s2, SD_BUFFER_INI

sdBusy:
    lbu     $t1, 0($s1)                     # $t1 = SDCtrl
    bne     $t1, $zero, sdBusy              # $t1 ? BUSY : READY

sdReadSector:
    sw      $a0, 0($s0)                     # &SD_INTERFACE_ADDR = $a0
    sw      $a0, 0($s0)                     # &SD_INTERFACE_ADDR = $a0          // XXX: Vai que, né?

sdWaitRead:
    lbu     $t1, 0($s1)                     # $t1 = SDCtrl
    bne     $t1, $zero, sdWaitRead          # $t1 ? BUSY : READY

    li      $t0, 512                        # Tamanho do buffer em bytes

sdDataReady:
    lw      $t2, 0($s2)                     # Lê word do buffer
    sw      $t2, 0($a1)                     # Salva word no destino
    addi    $s2, $s2, 4                     # Incrementa endereço do buffer
    addi    $a1, $a1, 4                     # Incrementa endereço de destino
    addi    $a2, $a2, -4                    # Decrementa quantidade de bytes a serem lidos
    addi    $t0, $t0, -4                    # Decrementa contador de bytes lidos no setor
    beq     $a2, $zero, sdFim               # Se leu todos os bytes desejados, finaliza
    bne     $t0, $zero, sdDataReady         # Lê próxima word

    addi    $a0, $a0, 512                   # Define endereço do próximo setor
    la      $s2, SD_BUFFER_INI              # Coloca o endereçamento do buffer na posição inicial
    j       sdReadSector

sdFim:
    li      $v0, 0                          # Sucesso na transferência.         NOTE: Hardcoded. Um teste de falha deve ser implementado.
    jr      $ra
############################################
